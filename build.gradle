import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

apply plugin: "java"
apply plugin: "maven-publish"
apply plugin: "signing"

group = "com.mayreh.lowtable"
version = "${version}" + (snapshot.toBoolean() ? "-SNAPSHOT" : "")

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    mavenCentral()
}

ext {
    lombokVersion = '1.18.22'
    isReleaseVersion = !version.endsWith('-SNAPSHOT')
}

dependencies {
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.assertj:assertj-core:3.21.0"
}

tasks.withType(Test) {
    testLogging {
        events TestLogEvent.FAILED,
                TestLogEvent.PASSED,
                TestLogEvent.SKIPPED,
                TestLogEvent.STANDARD_OUT
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true
        showStandardStreams false
    }
}

task javadocJar(type: Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

publishing {
    repositories {
        maven {
            if (isReleaseVersion) {
                url("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
            } else {
                url("https://oss.sonatype.org/content/repositories/snapshots/")
            }
            credentials {
                username findProperty("sonatypeUsername")
                password findProperty("sonatypePassword")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = project.name

            artifact javadocJar
            artifact sourcesJar

            pom {
                name = 'lowtable'
                description = 'Simple swing component to render tables'
                url = 'https://github.com/ocadaruma/lowtable'

                scm {
                    connection = 'scm:git:git@github.com:ocadaruma/lowtable.git'
                    url = 'git@github.com:ocadaruma/lowtable.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://raw.githubusercontent.com/ocadaruma/lowtable/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'ocadaruma'
                        name = 'Haruki Okada'
                    }
                }
            }
        }
    }
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
    sign publishing.publications.mavenJava
}
